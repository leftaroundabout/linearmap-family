-- |
-- Module      : Math.LinearMap.TypeFamily.Class
-- Copyright   : (c) Justus SagemÃ¼ller 2016
-- License     : GPL v3
-- 
-- Maintainer  : (@) sagemueller $ geo.uni-koeln.de
-- Stability   : experimental
-- Portability : portable
-- 
{-# LANGUAGE FlexibleInstances          #-}
{-# LANGUAGE FlexibleContexts           #-}
{-# LANGUAGE ConstraintKinds            #-}
{-# LANGUAGE UndecidableInstances       #-}
{-# LANGUAGE FunctionalDependencies     #-}
{-# LANGUAGE TypeOperators              #-}
{-# LANGUAGE TypeFamilies               #-}
{-# LANGUAGE ScopedTypeVariables        #-}
{-# LANGUAGE UnicodeSyntax              #-}
{-# LANGUAGE CPP                        #-}
{-# LANGUAGE TupleSections              #-}
{-# LANGUAGE StandaloneDeriving         #-}

module Math.LinearMap.TypeFamily.Class (LinearSpace (..)) where

import Data.VectorSpace

import Prelude ()
import qualified Prelude as Hask

import Control.Category.Constrained.Prelude
import Control.Arrow.Constrained

import Data.Tree (Tree(..), Forest)
import Data.List (sortBy, foldl')
import qualified Data.Set as Set
import Data.Set (Set)
import Data.Ord (comparing)
import Data.List (maximumBy)

import Data.VectorSpace.Free
import qualified Linear.Matrix as Mat
import qualified Linear.Vector as Mat

type Num' s = (Num s, VectorSpace s, Scalar s ~ s)

class (VectorSpace v, Num' (Scalar v)) => LinearSpace v where
  data (-â†’) v w :: *
  linearId :: v -â†’ v
  zeroMapping :: (LinearSpace w, Scalar w ~ Scalar v) => v -â†’ w
  addLinearMaps :: (LinearSpace w, Scalar w ~ Scalar v)
                => (v -â†’ w) -> (v -â†’ w) -> v -â†’ w
  subtractLinearMaps :: (LinearSpace w, Scalar w ~ Scalar v)
                => (v -â†’ w) -> (v -â†’ w) -> v -â†’ w
  subtractLinearMaps m n = addLinearMaps m (negateLinearMap n)
  scaleLinearMap :: (LinearSpace w, Scalar w ~ Scalar v)
                => Scalar v -> (v -â†’ w) -> v -â†’ w
  negateLinearMap :: (LinearSpace w, Scalar w ~ Scalar v)
                => (v -â†’ w) -> v -â†’ w
  linearCoFst :: (LinearSpace w, Scalar w ~ Scalar v)
                => v -â†’ (v,w)
  linearCoSnd :: (LinearSpace w, Scalar w ~ Scalar v)
                => v -â†’ (w,v)
  fanoutBlocks :: ( LinearSpace w, LinearSpace x
                , Scalar w ~ Scalar v, Scalar x ~ Scalar v )
     => (v-â†’w) -> (v-â†’x) -> v -â†’ (w,x)
  fstBlock :: ( LinearSpace w, LinearSpace x
                , Scalar w ~ Scalar v, Scalar x ~ Scalar v )
     => (v-â†’(w,x)) -> v -â†’ w
  sndBlock :: ( LinearSpace w, LinearSpace x
                , Scalar w ~ Scalar v, Scalar x ~ Scalar v )
     => (v-â†’(w,x)) -> v -â†’ x
  sepBlocks :: ( LinearSpace w, LinearSpace x
                , Scalar w ~ Scalar v, Scalar x ~ Scalar v )
     => (v-â†’(w,x)) -> (v-â†’w, v-â†’x)
  sepBlocks m = (fstBlock m, sndBlock m)
  firstBlock :: ( LinearSpace w, LinearSpace x
                , Scalar w ~ Scalar v, Scalar x ~ Scalar v )
     => (v-â†’w) -> v -â†’ (w,x)
  secondBlock :: ( LinearSpace w, LinearSpace x
                      , Scalar w ~ Scalar v, Scalar x ~ Scalar v )
           => (v-â†’x) -> v -â†’ (w,x)
  applyLinear :: (LinearSpace w, Scalar w ~ Scalar v)
                => (v -â†’ w) -> v -> w
  composeLinear :: ( LinearSpace w, LinearSpace x
                   , Scalar w ~ Scalar v, Scalar x ~ Scalar v )
           => (w -â†’ x) -> (v -â†’ w) -> v -â†’ x



data ZeroDim s = Origin
instance Monoid (ZeroDim s) where
  mempty = Origin
  mappend Origin Origin = Origin

instance AdditiveGroup (ZeroDim s) where
  zeroV = Origin
  Origin ^+^ Origin = Origin
  negateV Origin = Origin
instance VectorSpace (ZeroDim s) where
  type Scalar (ZeroDim s) = s
  _ *^ Origin = Origin
instance Num' s => LinearSpace (ZeroDim s) where
  data ZeroDim s -â†’ v = CoOrigin
  linearId = CoOrigin
  zeroMapping = CoOrigin
  negateLinearMap CoOrigin = CoOrigin
  scaleLinearMap _ CoOrigin = CoOrigin
  addLinearMaps CoOrigin CoOrigin = CoOrigin
  subtractLinearMaps CoOrigin CoOrigin = CoOrigin
  linearCoFst = CoOrigin
  linearCoSnd = CoOrigin
  fstBlock CoOrigin = CoOrigin
  sndBlock CoOrigin = CoOrigin
  fanoutBlocks CoOrigin CoOrigin = CoOrigin
  firstBlock CoOrigin = CoOrigin
  secondBlock CoOrigin = CoOrigin
  applyLinear _ _ = zeroV
  composeLinear _ _ = CoOrigin


newtype LinearMap s v w = LinearMap {getLinearMap :: v -â†’ w}

instance (LinearSpace v, LinearSpace w, Scalar v~s, Scalar w~s)
               => AdditiveGroup (LinearMap s v w) where
  zeroV = LinearMap zeroMapping
  LinearMap f ^+^ LinearMap g = LinearMap $ addLinearMaps f g
  LinearMap f ^-^ LinearMap g = LinearMap $ subtractLinearMaps f g
  negateV (LinearMap f) = LinearMap $ negateLinearMap f
instance (LinearSpace v, LinearSpace w, Scalar v~s, Scalar w~s)
               => VectorSpace (LinearMap s v w) where
  type Scalar (LinearMap s v w) = s
  Î¼ *^ LinearMap f = LinearMap $ scaleLinearMap Î¼ f
instance Num (LinearMap â„ â„ â„) where
  fromInteger = LinearMap . RealVect . fromInteger
  (+) = (^+^)
  (-) = (^-^)
  LinearMap (RealVect m) * LinearMap (RealVect n)
         = LinearMap . RealVect $ m*n
  abs (LinearMap (RealVect n)) = LinearMap . RealVect $ abs n
  signum (LinearMap (RealVect n)) = LinearMap . RealVect $ signum n
instance Fractional (LinearMap â„ â„ â„) where
  fromRational = LinearMap . RealVect . fromRational
  LinearMap (RealVect m) / LinearMap (RealVect n)
         = LinearMap . RealVect $ m/n
  recip (LinearMap (RealVect n)) = LinearMap . RealVect $ recip n
  
infixr 6 âŠ•, >+<
(âŠ•), (>+<) :: LinearMap s u w -> LinearMap s v w -> LinearMap s (u,v) w
LinearMap m âŠ• LinearMap n = LinearMap $ CoDirectSum m n
(>+<) = (âŠ•)

instance Show (LinearMap â„ â„ â„) where
  showsPrec p (LinearMap (RealVect n)) = showsPrec p n
instance âˆ€ u v . (Show (LinearMap â„ u â„), Show (LinearMap â„ v â„))
           => Show (LinearMap â„ (u,v) â„) where
  showsPrec p (LinearMap (CoDirectSum m n))
        = showParen (p>6)
            (showsPrec 6 (LinearMap m :: LinearMap â„ u â„)
                         . ("âŠ•"++) . showsPrec 7 (LinearMap n :: LinearMap â„ v â„))
instance âˆ€ s u v w . ( LinearSpace u, LinearSpace v, LinearSpace w
                     , Scalar u ~ s, Scalar v ~ s, Scalar w ~ s
                     , Show (LinearMap s u v), Show (LinearMap s u w) )
           => Show (LinearMap s u (v,w)) where
  showsPrec p (LinearMap m)
        = showParen (p>6)
            (showsPrec 6 (LinearMap mv :: LinearMap s u v)
                         . (" &&& "++) . showsPrec 6 (LinearMap mw :: LinearMap s u w))
   where (mv, mw) = sepBlocks m

instance Category (LinearMap s) where
  type Object (LinearMap s) v = (LinearSpace v, Scalar v ~ s)
  id = LinearMap linearId
  LinearMap f . LinearMap g = LinearMap $ composeLinear f g
instance Num' s => Cartesian (LinearMap s) where
  type UnitObject (LinearMap s) = ZeroDim s
  swap = LinearMap $ CoDirectSum linearCoSnd linearCoFst
  attachUnit = LinearMap linearCoFst
  detachUnit = LinearMap $ CoDirectSum linearId zeroMapping
  regroup = LinearMap $ CoDirectSum (composeLinear linearCoFst linearCoFst)
                                    (CoDirectSum (composeLinear linearCoFst linearCoSnd)
                                                 linearCoSnd )
  regroup' = LinearMap $ CoDirectSum (CoDirectSum linearCoFst
                                                  (composeLinear linearCoSnd linearCoFst))
                                     (composeLinear linearCoSnd linearCoSnd)
instance Num' s => Morphism (LinearMap s) where
  LinearMap f *** LinearMap g
      = LinearMap $ CoDirectSum (firstBlock f) (secondBlock g)
instance Num' s => PreArrow (LinearMap s) where
  LinearMap f &&& LinearMap g = LinearMap $ fanoutBlocks f g
  terminal = zeroV
  fst = LinearMap $ CoDirectSum linearId zeroMapping
  snd = LinearMap $ CoDirectSum zeroMapping linearId
instance Num' s => EnhancedCat (->) (LinearMap s) where
  arr (LinearMap m) = applyLinear m

type â„ = Double

instance LinearSpace â„ where
  data â„ -â†’ w = RealVect w
  linearId = RealVect 1
  zeroMapping = RealVect zeroV
  scaleLinearMap Î¼ (RealVect v) = RealVect $ Î¼ *^ v
  addLinearMaps (RealVect v) (RealVect w) = RealVect $ v ^+^ w
  subtractLinearMaps (RealVect v) (RealVect w) = RealVect $ v ^-^ w
  negateLinearMap (RealVect w) = RealVect $ negateV w
  linearCoFst = RealVect (1, zeroV)
  linearCoSnd = RealVect (zeroV, 1)
  fstBlock (RealVect (u, v)) = RealVect u
  sndBlock (RealVect (u, v)) = RealVect v
  fanoutBlocks (RealVect v) (RealVect w) = RealVect (v,w)
  firstBlock (RealVect v) = RealVect (v,zeroV)
  secondBlock (RealVect w) = RealVect (zeroV,w)
  applyLinear (RealVect w) Î¼ = Î¼ *^ w
  composeLinear f (RealVect w) = RealVect $ applyLinear f w

#define FreeLinearSpace(V, LV)                          \
instance Num' s => LinearSpace (V s) where {             \
  data V s -â†’ w = LV (V w);                               \
  linearId = LV Mat.identity;                              \
  zeroMapping = LV $ pure zeroV;                            \
  addLinearMaps (LV m) (LV n) = LV $ liftA2 (^+^) m n;       \
  subtractLinearMaps (LV m) (LV n) = LV $ liftA2 (^-^) m n;   \
  negateLinearMap (LV m) = LV $ fmap negateV m;                \
  scaleLinearMap Î¼ (LV m) = LV $ fmap (Î¼*^) m;                  \
  linearCoFst = LV $ fmap (,zeroV) Mat.identity;                 \
  linearCoSnd = LV $ fmap (zeroV,) Mat.identity;                  \
  fstBlock (LV m) = LV $ fmap fst m;                               \
  sndBlock (LV m) = LV $ fmap snd m;                                \
  fanoutBlocks (LV m) (LV n) = LV $ liftA2 (,) m n;                  \
  firstBlock (LV m) = LV $ fmap (,zeroV) m;                           \
  secondBlock (LV m) = LV $ fmap (zeroV,) m;                           \
  applyLinear (LV m) v = foldl' (^+^) zeroV $ liftA2 (^*) m v;          \
  composeLinear f (LV m) = LV $ fmap (applyLinear f) m }
FreeLinearSpace(V0, FromV0)
FreeLinearSpace(V1, FromV1)
FreeLinearSpace(V2, FromV2)
FreeLinearSpace(V3, FromV3)
FreeLinearSpace(V4, FromV4)
  
instance âˆ€ u v . (LinearSpace u, LinearSpace v, Scalar u ~ Scalar v)
                       => LinearSpace (u,v) where
  data (u,v) -â†’ w = CoDirectSum !(u-â†’w) !(v-â†’w)
  linearId = CoDirectSum linearCoFst linearCoSnd
  zeroMapping = CoDirectSum zeroMapping zeroMapping
  scaleLinearMap Î¼ (CoDirectSum fu fv)
      = CoDirectSum (scaleLinearMap Î¼ fu) (scaleLinearMap Î¼ fv)
  addLinearMaps (CoDirectSum fu fv) (CoDirectSum fu' fv')
      = CoDirectSum (addLinearMaps fu fu') (addLinearMaps fv fv')
  subtractLinearMaps (CoDirectSum fu fv) (CoDirectSum fu' fv')
      = CoDirectSum (subtractLinearMaps fu fu') (subtractLinearMaps fv fv')
  negateLinearMap (CoDirectSum fu fv)
      = CoDirectSum (negateLinearMap fu) (negateLinearMap fv)
  linearCoFst = CoDirectSum (composeLinear linearCoFst linearCoFst)
                            (composeLinear linearCoFst linearCoSnd)
  linearCoSnd = CoDirectSum (composeLinear linearCoSnd linearCoFst)
                            (composeLinear linearCoSnd linearCoSnd)
  fstBlock (CoDirectSum fu fv) = CoDirectSum (fstBlock fu) (fstBlock fv)
  sndBlock (CoDirectSum fu fv) = CoDirectSum (sndBlock fu) (sndBlock fv)
  sepBlocks (CoDirectSum fu fv) = (CoDirectSum fuw fvw, CoDirectSum fux fvx)
   where (fuw,fux) = sepBlocks fu
         (fvw,fvx) = sepBlocks fv
  fanoutBlocks (CoDirectSum fu fv) (CoDirectSum gu gv)
              = CoDirectSum (fanoutBlocks fu gu) (fanoutBlocks fv gv)
  firstBlock (CoDirectSum fu fv) = CoDirectSum (firstBlock fu) (firstBlock fv)
  secondBlock (CoDirectSum fu fv) = CoDirectSum (secondBlock fu) (secondBlock fv)
  applyLinear (CoDirectSum fu fv) (u,v) = applyLinear fu u ^+^ applyLinear fv v
  composeLinear f (CoDirectSum fu fv)
        = CoDirectSum (composeLinear f fu) (composeLinear f fv)

lfstBlock :: ( LinearSpace u, LinearSpace v, LinearSpace w
             , Scalar u ~ Scalar v, Scalar v ~ Scalar w )
          => (u-â†’w) -> (u,v)-â†’w
lfstBlock f = CoDirectSum f zeroMapping
lsndBlock :: ( LinearSpace u, LinearSpace v, LinearSpace w
            , Scalar u ~ Scalar v, Scalar v ~ Scalar w )
          => (v-â†’w) -> (u,v)-â†’w
lsndBlock f = CoDirectSum zeroMapping f

type DualSpace v = LinearMap (Scalar v) v (Scalar v)

type Fractional' s = (Fractional s, Eq s, VectorSpace s, Scalar s ~ s)

class (LinearSpace v, LinearSpace (Scalar v)) => SemiInner v where
  -- | Lazily enumerate choices of a basis of functionals that can be made dual
  --   to the given vectors, in order of preference (which roughly means, /large in
  --   the normal direction/. (I.e., if the vector @ð‘£@ is assigned early to the
  --   dual vector @ð‘£'@, then @ð‘£' $ ð‘£$ should be large and all the other products
  --   comparably small. The purpose is that we should be able to make this basis
  --   orthonormal with a ~Gaussian-elimination approach, in a way that stays
  --   numerically stable. It is essentially the choice of a pivot element.)
  -- 
  --   For simple finite-dimensional array-vectors, you can easily define this
  --   method using 'cartesianDualBasisCandidates'.
  dualBasisCandidates :: [(Int,v)] -> Forest (Int, v -â†’ Scalar v)
--  coDualBasis :: [(i,DualSpace v)] -> [(i,v)]

cartesianDualBasisCandidates
     :: [v-â†’Scalar v]   -- ^ Set of canonical basis functionals.
     -> (v -> [â„])      -- ^ Decompose a vector in /absolute value/ components.
                        --   the list indices should correspond to those in
                        --   the functional list.
     -> ([(Int,v)] -> Forest (Int, v -â†’ Scalar v))
                        -- ^ Suitable definition of 'dualBasisCandidates'.
cartesianDualBasisCandidates dvs abss vcas = go 0 sorted
 where sorted = sortBy (comparing $ negate . snd . snd)
                       [ (i, (av, maximum av)) | (i,v)<-vcas, let av = abss v ]
       go k ((i,(av,_)):scs)
          | k<n   = Node (i, dv) (go (k+1) [(i',(zeroAt j av',m)) | (i',(av',m))<-scs])
                                : go k scs
          | otherwise = []
        where (j,_) = maximumBy (comparing snd) $ zip jfus av
              dv = dvs !! j
       
       jfus = [0 .. n-1]
       n = length dvs
       
       zeroAt :: Int -> [â„] -> [â„]
       zeroAt _ [] = []
       zeroAt 0 (_:l) = (-1/0):l
       zeroAt j (e:l) = e : zeroAt (j-1) l

instance (Fractional' s, SemiInner s) => SemiInner (ZeroDim s) where
  dualBasisCandidates _ = []

orthonormaliseDuals :: (SemiInner v, Fractional (Scalar v))
                          => [(v, DualSpace v)] -> [(v,DualSpace v)]
orthonormaliseDuals [] = []
orthonormaliseDuals ((v,v'â‚€):ws) = (v,v') : [(w, w' ^-^ (w'$v)*^v') | (w,w')<-wssys]
 where wssys = orthonormaliseDuals ws
       v'â‚ = foldl' (\v'i (w,w') -> v'i ^-^ (v'i$w)*^w') v'â‚€ wssys
       v' = v'â‚ ^/ (v'â‚$v)

dualBasis :: (SemiInner v, Fractional (Scalar v)) => [v] -> [DualSpace v]
dualBasis vs = snd <$> orthonormaliseDuals (zip' vsIxed candidates)
 where zip' ((i,v):vs) ((j,v'):ds)
        | i<j   = zip' vs ((j,v'):ds)
        | i==j  = (v,v') : zip' vs ds
       zip' _ _ = []
       candidates = map (second LinearMap) . sortBy (comparing fst) . findBest
                             $ dualBasisCandidates vsIxed
        where findBest [] = []
              findBest (Node iv' bv' : _) = iv' : findBest bv'
       vsIxed = zip [0..] vs

instance SemiInner â„ where
--   (^/^) = (/)
--   recipV = RealVect . recip
  dualBasisCandidates = fmap ((`Node`[]) . second (RealVect . recip))
                . sortBy (comparing $ negate . abs . snd)
                . filter ((/=0) . snd)

instance (SemiInner u, SemiInner v, Scalar u ~ Scalar v) => SemiInner (u,v) where
  dualBasisCandidates = fmap (\(i,(u,v))->((i,u),(i,v))) >>> unzip
              >>> dualBasisCandidates *** dualBasisCandidates
              >>> combineBaseis False mempty
   where combineBaseis _ _ ([], []) = []
         combineBaseis False forbidden (Node (i,du) bu' : abu, bv)
            | i`Set.member`forbidden  = combineBaseis False forbidden (abu, bv)
            | otherwise
                 = Node (i, lfstBlock du)
                        (combineBaseis True (Set.insert i forbidden) (bu', bv))
                       : combineBaseis False forbidden (abu, bv)
         combineBaseis True forbidden (bu, Node (i,dv) bv' : abv)
            | i`Set.member`forbidden  = combineBaseis True forbidden (bu, abv)
            | otherwise
                 = Node (i, lsndBlock dv)
                        (combineBaseis False (Set.insert i forbidden) (bu, bv'))
                       : combineBaseis True forbidden (bu, abv)
         combineBaseis _ forbidden (bu, []) = combineBaseis False forbidden (bu,[])
         combineBaseis _ forbidden ([], bv) = combineBaseis True forbidden ([],bv)
  
(^/^) :: (InnerSpace v, Eq (Scalar v), Fractional (Scalar v)) => v -> v -> Scalar v
v^/^w = case (v<.>w) of
   0 -> 0
   vw -> vw / (w<.>w)

class (LinearSpace v, LinearSpace (Scalar v)) => FiniteDimensional v where
  -- | For spaces with a canonical finite basis, this need not contain any
  --   information.
  data EntireBasis v :: *
  
  -- | Split up a linear map in â€œcolumn vectorsâ€ WRT some suitable basis.
  decomposeLinMap :: (v-â†’w) -> (EntireBasis v, [w]->[w])
  
  recomposeEntire :: EntireBasis v -> [Scalar v] -> (v, [Scalar v])
  
  recomposeContraLinMap :: (LinearSpace w, Scalar w ~ Scalar v, Hask.Functor f)
           => (f (Scalar w) -> w) -> f (DualSpace v) -> v-â†’w
  


instance (Num' s, LinearSpace s) => FiniteDimensional (ZeroDim s) where
  data EntireBasis (ZeroDim s) = ZeroBasis
  recomposeEntire ZeroBasis l = (Origin, l)
  decomposeLinMap _ = (ZeroBasis, id)
  recomposeContraLinMap _ _ = CoOrigin
  
instance FiniteDimensional â„ where
  data EntireBasis â„ = RealsBasis
  recomposeEntire RealsBasis [] = (0, [])
  recomposeEntire RealsBasis (Î¼:cs) = (Î¼, cs)
  decomposeLinMap (RealVect v) = (RealsBasis, (v:))
  recomposeContraLinMap fw = RealVect . fw . fmap ($1)

deriving instance Show (EntireBasis â„)
  
instance ( FiniteDimensional u, InnerSpace u, FiniteDimensional v, InnerSpace v
         , Scalar u ~ Scalar v, Fractional' (Scalar v) )
            => FiniteDimensional (u,v) where
  data EntireBasis (u,v) = TupleBasis !(EntireBasis u) !(EntireBasis v)
  decomposeLinMap (CoDirectSum fu fv) = case (decomposeLinMap fu, decomposeLinMap fv) of
         ((bu, du), (bv, dv)) -> (TupleBasis bu bv, du . dv)
  recomposeEntire (TupleBasis bu bv) coefs = case recomposeEntire bu coefs of
                        (u, coefs') -> case recomposeEntire bv coefs' of
                         (v, coefs'') -> ((u,v), coefs'')
  recomposeContraLinMap fw dds
         = CoDirectSum (recomposeContraLinMap fw 
                         $ fmap (\(LinearMap (CoDirectSum v' _)) -> LinearMap v') dds)
                       (recomposeContraLinMap fw
                         $ fmap (\(LinearMap (CoDirectSum _ v')) -> LinearMap v') dds)
  
deriving instance (Show (EntireBasis u), Show (EntireBasis v))
                    => Show (EntireBasis (u,v))

infixr 0 \$

(\$) :: ( FiniteDimensional u, FiniteDimensional v, SemiInner v
        , Scalar u ~ Scalar v, Fractional (Scalar v) )
          => LinearMap s u v -> v -> u
(\$) (LinearMap m) = fst . \v -> recomposeEntire mbas [v' $ v | v' <- v's]
 where v's = dualBasis $ mdecomp []
       (mbas, mdecomp) = decomposeLinMap m
    

pseudoInverse :: ( FiniteDimensional u, FiniteDimensional v, SemiInner v
        , Scalar u ~ Scalar v, Fractional (Scalar v) )
          => LinearMap s u v -> LinearMap s v u
pseudoInverse (LinearMap m) = LinearMap mi
 where mi = recomposeContraLinMap (fst . recomposeEntire mbas) v's
       v's = dualBasis $ mdecomp []
       (mbas, mdecomp) = decomposeLinMap m
